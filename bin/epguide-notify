#!/usr/bin/perl -w

package EpisodeGuide;

use strict;
use warnings FATAL => 'all';

use DateTime;
use Getopt::Long;
use Log::Log4perl;
use EpisodeGuide::EpisodeGuides;

# Configuration log4perl
my $user_logConfig   = $ENV{HOME} . '/.epguide-util/log4perl.conf';
if (-f $user_logConfig) {
    Log::Log4perl::init($user_logConfig);
} else {
    Log::Log4perl::init();
}

my $LOGGER = Log::Log4perl->get_logger();
my $CFG    = parseCMDL();

sub parseCMDL { # {{{1
    my $config = $ENV{HOME} . '/.epguide-util/epguide.conf';
    my $help   = 0;

    my $result = GetOptions(
        "config=s"  => \$config,
        "help!"     => \$help,
    );

    if ($help) {
        print $0 . ': <OPTIONS>' . "\n";
        print "    --config:   filename of the configuration-file to read.\n";
        print "    --help:     print this help.\n";
        exit 0;
    }

    my %CFG;
    # read in $config file
    open(FILE, $config) or die "could not open $config: $!";
    my $content; { local $/ = undef; $content = <FILE> }
    close(FILE);
    eval $content or die "could not slurp in $config: $!";

    return \%CFG;
} # }}}1

my $epGuides  = EpisodeGuide::EpisodeGuides->new();

my $prefix    = $CFG->{'prefix'};
my $suffix    = $CFG->{'suffix'};
my $separator = $CFG->{'separator'};
my $today     = DateTime->now->ymd;

for my $serie (@{$CFG->{'series'}}) {
    my $serieID = $serie->{'id'};
    my $name = $serie->{'name'};
    my $searchName = $name;
    $searchName =~ s/\W+/$separator/g;

    $LOGGER->info('Processing ' . $name . ' with id ' . $serieID);
    my $episodes = $epGuides->getAllEpisodes($serieID);
    for my $episode (@$episodes) {
        my $eid     = $episode->{'id'};
        my $airDate = $episode->{'date'};

        if (($airDate cmp $today) <= 0) {
            print($airDate . ': ' . $name . ' [' . $eid . '] "' . $prefix . $searchName . '+' . uc($eid) . $suffix . "\"\n");
        }
    }
}
